plugins {
    id 'java'
    id 'checkstyle'
    id "com.github.spotbugs" version "1.6.9"
    id 'com.bmuschko.nexus' version '2.3.1'          // prepares and copies artifacts to Sonotype OSS
    id "io.codearte.nexus-staging" version "0.20.0"  // logs into Sonotype OSS and does a "Close" and "Release"
}

group = 'com.imsweb'
version = '1.5-SNAPSHOT'
description = 'Java client library for parsing x12 files'

// UTF-8 for all compilation tasks
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.thoughtworks.xstream:xstream:1.4.11.1'

    testCompile 'junit:junit:4.12'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.x12'
        )
    }
}

checkstyle {
    configFile = file('config/checkstyle/checkstyle.xml')
}

spotbugs {
    toolVersion = '3.1.11'
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

modifyPom {
    project {
        name 'X12 Parser'
        description 'A Java library for parsing X12 files, including ANSI 837'
        url 'https://github.com/imsweb/x12-parser'
        inceptionYear '2015'

        scm {
            url 'https://github.com/imsweb/x12-parser'
            connection 'scm:https://github.com/imsweb/x12-parser.git'
            developerConnection 'scm:git@github.com:imsweb/x12-parser.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/x12-parser/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'AngelaszekD'
                name 'David Angelaszek'
                email 'AngelaszekD@imsweb.com'
            }
            developer {
                id 'ctmay4'
                name 'Chuck May'
                email 'mayc@imsweb.com'
            }
        }
    }
}

wrapper {
    gradleVersion = '5.2.1'
    distributionType = Wrapper.DistributionType.ALL
}
